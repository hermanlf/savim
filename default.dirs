"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Initialize directories for swap, undo, backup and views
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

function! InitializeDirectories()
    let separator = "."
    let parent = $HOME
    let vimdir = '.vim'
    let prefix = '.vim'

    if VimVer() > 703
        set undofile
        set undolevels=1000
        set undoreload=10000
        let dir_list = { 
                    \ 'backup': 'backupdir', 
                    \ 'views': 'viewdir', 
                    \ 'swap': 'directory',
                    \ 'undo': 'undodir' }
    else
        let dir_list = { 
                    \ 'backup': 'backupdir', 
                    \ 'views': 'viewdir', 
                    \ 'swap': 'directory' }
    endif    


    for [dirname, settingname] in items(dir_list)
        let directory = parent . '/' . vimdir . '/' . prefix . dirname . "/"
        if exists("*mkdir")
            if !isdirectory(directory)
                call mkdir(directory)
            endif
        endif
        if !isdirectory(directory)
            echo "Warning: Unable to create backup directory: " . directory
            echo "Try: mkdir -p " . directory
        else  
            let directory = substitute(directory, " ", "\\\\ ", "")
            exec "set " . settingname . "=" . directory . "//"
        endif
    endfor
endfunction
call InitializeDirectories()
